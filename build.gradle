// Copyright 2017 Sourcerer Inc. All Rights Reserved.

buildscript {
    ext.kotlin_version = '1.1.4-2'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.1"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-RC2"
    }
}

plugins {
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

apply plugin: "idea"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "com.google.protobuf"
apply plugin: "org.junit.platform.gradle.plugin"

buildConfig {
    clsName = 'BuildConfig'
    packageName = 'app'
    buildConfigField 'int', 'VERSION_CODE', '1'
    buildConfigField 'String', 'VERSION', '0.0.1'
    buildConfigField "String", "PROFILE_URL", "https://sourcerer.io/"
    buildConfigField "String", "API_BASE_URL", "http://localhost:3181"
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

mainClassName = "app.MainKt"

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "com.beust:jcommander:1.72"
    compile 'com.google.protobuf:protobuf-java:3.0.0'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.9'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.9'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.9'
    compile "io.reactivex.rxjava2:rxjava:2.1.1"
    compile 'com.github.kittinunf.fuel:fuel:1.9.0'
    compile 'com.github.kittinunf.fuel:fuel-rxjava:1.9.0'
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit',
            version: '4.8.0.201706111038-r'

    testCompile 'org.jetbrains.kotlin:kotlin-test'
    testCompile 'org.jetbrains.spek:spek-api:1.1.4'
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-RC2'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.4'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
}

sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/java'
sourceSets.test.java.srcDirs += 'src/test/kotlin'
compileKotlin.dependsOn ':generateProto'

// Include dependent libraries in archive.
jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}
